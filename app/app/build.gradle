plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()
// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

def vMajor = 1
def vMinor = 0
def buildNum = System.getenv("BUILD_NUMBER") ?: 0
def fourDigitBuildnumber = System.getenv("FOURDIGIT_BUILD_NUMBER") ?: 0
def shVersionCode = (vMajor * 10000000) + (vMinor * 10000) + (buildNum as Integer)

android {
    compileSdkVersion 30
    buildToolsVersion "29.0.3"

    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    defaultConfig {
        applicationId "io.esper.sdksample"
        minSdkVersion 19
        targetSdkVersion 30
        versionCode shVersionCode as Integer
        versionName "v${vMajor}.${vMinor}.${fourDigitBuildnumber}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }

    buildFeatures {
        viewBinding = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.core:core-ktx:$core_ktx_version"

    // AppCompat
    implementation "androidx.appcompat:appcompat:$appcompat_version"

    // Material
    implementation "com.google.android.material:material:$material_version"

    // Constraint Layout
    implementation "androidx.constraintlayout:constraintlayout:$constraint_layout_version"

    // Esper SDK
    implementation "io.esper.devicesdk:app:$esper_sdk_version"

    // Test Utilities
    testImplementation "junit:junit:$junit_version"
    androidTestImplementation "androidx.test.ext:junit:$ext_junit_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_core_version"
}